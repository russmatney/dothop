@tool
extends Resource
class_name ParsedGame

@export var prelude: Dictionary
@export var legend: Dictionary
@export var puzzles: Array

static func parse(contents: String) -> ParsedGame:
	var parsed: ParsedGame = ParsedGame.new()

	# force a similar prelude header
	contents = "=======\nPRELUDE\n=======\n\n" + contents
	var raw_sections := contents.split("\n\n=")
	for raw_s: String in raw_sections:
		var chunks: Array = Array(raw_s.split("\n\n"))
		var header_rows: Array = (chunks[0] as String).split("\n")
		chunks.remove_at(0)
		var header: String = header_rows[1] # middle line
		var section: String = header.to_lower()
		var parser: Callable
		match section:
			"prelude": parser = parsed.parse_prelude
			"legend": parser = parsed.parse_legend
			"puzzles": parser = parsed.parse_puzzles
		if parser:
			var chunkses: Array = Array(chunks).map(func(c: String) -> Array: return c.split("\n"))
			var trimmed_chunks := chunkses.map(func(chunk_lines: Array) -> Array:
				return chunk_lines.filter(func(c: String) -> bool:
					return c != "" and c != "\t" and c != "\t\t" and c != "\t\t\t")
				).filter(func(chunk: Array) -> bool: return len(chunk) > 0)
			var result: Variant = parser.call(trimmed_chunks)
			match section:
				"prelude": parsed.prelude = result
				"legend": parsed.legend = result
				"puzzles": parsed.puzzles = result
	return parsed

func parse_metadata(lines: Array) -> Dictionary:
	var data := {}
	for l: String in lines:
		if l == "":
			continue
		var parts := l.split(" ", false, 1)
		var key := parts[0]
		parts.remove_at(0)
		var val: Variant = true
		if parts.size() > 0:
			val = " ".join(parts)
		data[key] = val
	return data

## prelude #########################################################

func parse_prelude(chunks: Array) -> Dictionary:
	var lines: Array = chunks.reduce(func(acc: Array, x: Array) -> Array:
		acc.append_array(x)
		# fking update-in-place with no return bs
		return acc, [])
	var lude := {}
	for l: String in lines:
		if l == "":
			continue
		var parts := l.split(" ", false, 1)
		var key := parts[0]
		parts.remove_at(0)
		var val: Variant = true
		if parts.size() > 0:
			val = " ".join(parts)
		lude[key] = val
	return lude

## legend #########################################################

func parse_legend(chunks: Array) -> Dictionary:
	var gend := {}
	for lines: Array in chunks:
		for l: String in lines:
			var parts := l.split(" = ")
			# support 'or' ?
			var val_parts := parts[1].split(" and ")
			gend[parts[0]] = Array(val_parts)

	return gend

## puzzles #########################################################

func parse_shape(lines: Array, parse_int: bool = false) -> Array:
	var shape := []
	for l: String in lines:
		var row := []
		for c in l:
			if c == ".":
				row.append(null)
			elif parse_int:
				row.append(int(c))
			else:
				row.append(c)
		if row.size() > 0:
			shape.append(row)
	return shape

func parse_puzzle(shape_lines: Array, raw_meta: Array = []) -> Dictionary:
	var meta := parse_metadata(raw_meta)
	var raw_shape: Variant = null
	if shape_lines:
		raw_shape = parse_shape(shape_lines)
	var line_count: int = 0
	if shape_lines:
		line_count = len(shape_lines)

	var puzzle := {
		meta=meta,
		shape=raw_shape,
		height=line_count,
		}

	if line_count > 0:
		puzzle["width"] = len(shape_lines[0])
	else:
		puzzle["width"] = 0
	# maybe want to optimize away from this?
	return puzzle.duplicate(true)

func parse_puzzles(chunks: Array) -> Array:
	var zles: Array = []
	var skip := false
	for i in len(chunks):
		if skip:
			skip = false
			continue
		# NOTE this is precarious and weird!!
		if chunks[i][0][0] in [".", "#", "a", "b", "o", "t", "x", "y"]:
			zles.append(parse_puzzle(chunks[i] as Array))
			skip = false
		else:
			var raw_meta: Array = chunks[i]
			var shape_lines: Array = []
			if i + 1 < len(chunks):
				shape_lines = chunks[i+1]
			zles.append(parse_puzzle(shape_lines, raw_meta))
			skip = true
	for i in len(zles):
		zles[i].idx = i
	return zles
